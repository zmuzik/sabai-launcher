apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

ext {
    supportLibVersion = '25.3.0'
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return 1;
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    Properties customProps = new Properties()
    customProps.load(new FileInputStream("../keystore/properties"))

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode getVersionCode()
        versionName "0.1"

        applicationId 'com.sabailabs.launcher'
        manifestPlaceholders = [authority: "${applicationId}.settings"]

        buildConfigField "String", "AUTHORITY", "\"${manifestPlaceholders.authority}\""
        buildConfigField "boolean", "IS_DOGFOOD_BUILD", "false"

        testApplicationId 'com.android.launcher3.tests'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        archivesBaseName = "slauncher-$versionCode"
    }

    signingConfigs {
        release {
            storeFile file("../../keystore/keystore")
            keyAlias customProps.KEY_ALIAS
            storePassword customProps.STORE_PASSWORD
            keyPassword customProps.KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            manifestPlaceholders = [authority: "com.sabailabs.launcher.debug.settings"]
            buildConfigField "String", "AUTHORITY", "\"com.sabailabs.launcher.debug.settings\""
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:palette-v7:${supportLibVersion}"
    compile "com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-2"

    testCompile "junit:junit:4.12"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.uiautomator:uiautomator-v18:2.1.2"
    androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option 'ignore_services=false'
                    }
                }
            }
        }
    }
}
