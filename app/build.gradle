apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return 1
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    def propsFile = rootProject.file("../secrets/properties")
    Properties props = new Properties()
    props.load(new FileInputStream(propsFile))


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName "1.2"

        applicationId 'com.sabailabs.launcher'
        manifestPlaceholders = [authority: "${applicationId}.settings"]

        buildConfigField "String", "AUTHORITY", "\"${manifestPlaceholders.authority}\""
        buildConfigField "boolean", "IS_DOGFOOD_BUILD", "false"

        testApplicationId 'com.android.launcher3.tests'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        archivesBaseName = "slauncher-$versionCode"
    }

    signingConfigs {
        release {
            storeFile file("../../secrets/keystore")
            keyAlias props.KEY_ALIAS
            storePassword props.STORE_PASSWORD
            keyPassword props.KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            manifestPlaceholders = [authority: "com.sabailabs.launcher.debug.settings"]
            buildConfigField "String", "AUTHORITY", "\"com.sabailabs.launcher.debug.settings\""
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

repositories {
    google()
    mavenCentral()
}

ext {
    supportLibVersion = '28.0.0'
}

dependencies {
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:palette-v7:${supportLibVersion}"
    implementation "com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-2"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:0.5"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:2.1.2"
    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option 'ignore_services=false'
                    }
                }
            }
        }
    }
}
